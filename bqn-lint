#!/usr/bin/env bqn

⟨Green, Yellow, Red⟩ ⇐ •Import "../bqn-color/color.bqn"
s  ⇐ •Import "../bqn-code/lib/string.bqn"
fs ⇐ •Import "../bqn-code/lib/file_system.bqn"
fn ⇐ •Import "../bqn-code/lib/fun.bqn"

nl ← @+10

title ← {
    c ← Green‿Yellow‿Red # colors
    a ← "↑↑↑"            # arrows
    nl∾(∾c{𝕎𝕩}¨a)∾Green " bqn-test "∾(∾c{𝕎𝕩}¨○⌽a)∾nl
}

replacements ← ⟨⟨"0≠≠", "×≠"⟩⟩ # TODO add more

CheckReplacement ← { f‿(t‿r)𝕊l‿i: 
    m ← t⍷l
    { ¬∨´m ? "" ; 
        •Out "In "∾f∾":"
        •Out "  Line "∾(•Fmt i)∾":"
        •Out "    "∾(Red t) s.Join t s.Split l
        •Out "    "∾ Yellow " ↑"⊏˜t fn.FindFull l
        •Out "    "∾(" "/˜⊑/m)∾(Yellow "this can be: ")∾Green r
        •Out ⟨⟩
    }
}

LintSingleFile ← { 𝕊file:
    l ← •file.Lines 𝕩
    {(file⋈𝕩)⊸CheckReplacement¨(⋈¨⟜(↕∘≠)l)}¨replacements
}

main ← {
    •Out title
    (Red "Must provide directory (`pwd` for current)") ! 0≠≠•args
    d ← 0⊑•args  # directory
    f ← fs.ListRecursiveBQNFiles d
    LintSingleFile¨f
}

main